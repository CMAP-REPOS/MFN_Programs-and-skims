
~# 2_BUILD_NETWORK.MAC
~#   Craig Heither, last revised 06-14-2017
~#
~#  ************************************************************************************************
~#    Macro builds a scenario network for the Meso Freight Model skims.
~#    [Based on CS's macro MesoSkims.mac]
~#
~#    Revisions:
~#      07-31-2015: Revised code to implement new POE codes for highway and rail.
~#      04-18-2016: Lafayette Cruise revised code to create @mzone, @lenfactor, and @domestic    
~#      10-03-2016: Craig Heither revised code to create @lhdist   
~#      05-16-2017: Craig Heither, capture rail dwell time at selected cities & store in us2           
~#
~#  - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
~#    Must provide 3-digit scenario and logistics node 140 inclusion flag when submitting macro. 
~#          %1% = 3-digit scenario
~#          %2% = include logistics node 140? 0: no (input file to delete 140 connectors), 1: yes
~#          %3% = include logistics node 143? 0: no (input file to delete 143 connectors), 1: yes
~# 	(e.g. "~<macros\2_build_network.mac 100 0")
~#
~#  ************************************************************************************************
~# =================================================================================================
~#
~z=%0%
~+;~?!z=3;~$>error
~#
on=25                       /set to 4-digit date
~#
~# ##########################################################
~#     --- Set Path to Batchin Files ---
~t1=input_data
~#
~#     --- Set Report ---
~t2=reports\build_scenario_%1%.rpt
~#
~#     --- Set Scenario Name ---
~t3=Base Meso Freight Network - %d%
~#
~#
~# ## -- Rail Dwell Times for Selected East-West Gateways -- ##
~# ## -- (code assigned in descending order of Rail Dwell Times) -- ##
~r1=183        / St. Louis mesozone
~r2=210        / St. Louis mesozone
~r3=3          / St. Louis code
~r4=249        / Memphis mesozone
~r5=1          / Memphis code
~r6=195        / New Orleans mesozone
~r7=2          / New Orleans code
~r8=189        / Kansas City mesozone
~r9=209        / Kansas City mesozone
~r10=4         / Kansas City code
~#
~# ##########################################################
~#
~o|39
~#
~!if exist %t2% (del %t2%)
reports=%t2%
~/
~# ______________________________________________
~/ ## -- Build Network %1%: %t3% -- ##
~# ______________________________________________
s=%1%
~+;~?e;~$>new
~+;1.22;2;%1%;y;q
~:new
~+;1.22;1;%1%;%t3%;y;q
~#
~#  -- Modes and transit vehicles --
batchin=%t1%\modes.in
~+;2.01;1;2;q
batchin=%t1%\vehicles.in
~+;2.02;1;2;q
~#
~#  -- Network --
batchin=%t1%\base_ntwk.txt
~+;2.11;2;
~#
~#  -- Logistics Node 140 connectors --
~z=%2%
~+;~?z>0;~$>leave_140connect
batchin=%t1%\unlink_lognode140.txt
~+;2.11;2;
~/
~:leave_140connect
~#
~#  -- Logistics Node 143 connectors --
~z=%3%
~+;~?z>0;~$>leave_143connect
batchin=%t1%\unlink_lognode143.txt
~+;2.11;2;
~/
~:leave_143connect
~#
~#  -- Transit Lines --
batchin=%t1%\lines.in
~+;2.21;2
~#
~#  -- Volume-Delay Functions --
batchin=%t1%\vdf.in
~+;4.11;2;
~#
~#
~# ______________________________________________
~/ ## -- Add Extra Attributes -- ##
~# ______________________________________________
~#  -- Assign POE codes for CMAP entry/exit --
batchin=%t1%\poe.in
2.42
~+;2;1;@poecd;poe code number;0
~+;2;2;@poe;poe code for skims;0;q
2.41
~+;3;@poecd;n;all;1,2;n;y
~+;1;y;@poe;n;@poecd; ;all;2;2;q
~#
~#  -- Set us1 value (minutes per mile)
2.41
~+;1;y;us1;60/speed; ;all;all;4;q
~#
~#
~#
~#  -- Report Data --
2.41
~+;1;n;@poe; ;all;2;2;q
~# ~+;1;n;@rline; ;all;2;2;q
~+;2.14; 3; 2; q
~#
~#
~#========= Extra Attribute edits created by Lafayette Cruise on 04/18/16 =========
~#
~+;2.42;2;1;@mzone;mesozone in which node is located;0;q
~+;2.41;1;y;@mzone;n;ui1; ;all;4;q
~#
~#  -- Domestic Distance --
batchin=%t1%\DomesticNetwork.csv
~+;2.42;2;2;@domestic;domestic distance;0;q
~+;2.41;3;@domestic;n;all;1,2,5;n;y;q
~+;2.41;3;@domestic;n;all;2,1,5;n;y;q
~#
~#
~+;2.42;2;2;@lhdist;linehaul(ivtt) distance;0;q
~+;2.41;1;y;@lhdist;n;(i.gt.399 .and. j.gt.399)*len; ;all;4;q
~#
~#========= Rail Dwell Time Extra Attribute code - 05-16-2017 =========
~+;2.42;2;1;@rdwell;rail dwell time code;0;q
2.41
~+;1;y;@rdwell;n;(@mzone.eq.%r1% .or. @mzone.eq.%r2%)*%r3% +
(@mzone.eq.%r4%)*%r5% +
(@mzone.eq.%r6%)*%r7% +
(@mzone.eq.%r8% .or. @mzone.eq.%r9%)*%r10%
~+; ;all;4;q
~#=================================================================================
~# ______________________________________________
~# ## -- Pipeline Network -- ##
~# ______________________________________________
~+;~x=%s%;~x+10
~+;~r1=%1%;~r1+10
~+;~r2=%1%;~r2+11
~+;~r3=%1%;~r3+12
~+;~r4=%1%;~r4+13
~#
~t2=reports\build_scenario_%x%.rpt
~!if exist %t2% (del %t2%)
reports=%t2%
~#
~:pipeline_loop
~?x=%r1%
~+;~t2=cos_ntwk.txt;~t4=Crude Oil Pipeline Network - %d%
~?x=%r2%
~+;~t2=p1718_ntwk.txt;~t4=Petroleum Products Pipeline Network - %d%
~?x=%r3%
~+;~t2=nec_19_ntwk.txt;~t4=Coal N.E.C. Pipeline Network - %d%
~#
s=%x%
~+;~?e;~$>new
~+;1.22;2;%x%;y;q
~:new
~+;1.22;1;%x%;%t4%;y;q
~#
~/ ## -- Build Network %x%: %t4% -- ##
batchin=%t1%\modes.in
~+;2.01;1;2;q
batchin=%t1%\%t2%
~+;2.11;2;
~#
~/ ## -- Add Extra Attributes -- ##
~+;2.42;2;1;@mzone;mesozone in which node is located;0;q
~+;2.41;1;y;@mzone;n;ui1; ;all;4;q
~#  -- Domestic Distance --
batchin=%t1%\DomesticPipelineNetwork.csv
~+;2.42;2;2;@domestic;domestic distance;0;q
~+;2.41;3;@domestic;n;all;1,2,5;n;y;q
~+;2.41;3;@domestic;n;all;2,1,5;n;y;q
~#
~#
~x+1
~+;~?x<%r4%;~$pipeline_loop
~## 
~# ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~#  Heither 09-20-2017: quick fix for @domestic for CoalNEC ntwk
~#  Heither 02-07-2019: quick fix no longer necessary, @domestic included in batchin files
~# ## +;2.41;1;y;@domestic;n;(@mzone.le.273)*len; ;all;4;q
~# #######
~#=================================================================================
~#
~$>end
~#
~:error
~/ +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~/   SUBMIT WITH 3-DIGIT SCENARIO & LOGISTICS NODE 140 INCLUSION FLAG !!!!!!
~/ +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~$>end
~#
~:end
~o=6
reports=
batchin=
~/ -- end of macro --
q
